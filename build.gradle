import io.netifi.flatbuffers.plugin.tasks.FlatBuffers

plugins {
    id 'java'
    id 'maven-publish'
    id "io.netifi.flatbuffers" version "1.0.7"
}

group 'net.uberfoo.cpm'
version '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.jetbrains:annotations:23.0.0'
    implementation 'org.slf4j:slf4j-api:2.0.6'
    implementation 'com.google.flatbuffers:flatbuffers-java:23.1.21'

    testImplementation(platform('org.apache.logging.log4j:log4j-bom:2.19.0'))

    testImplementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.14.1'
    testImplementation 'com.fasterxml.jackson.core:jackson-core:2.14.1'
    testImplementation 'org.apache.logging.log4j:log4j-api'

    testRuntimeOnly 'org.apache.logging.log4j:log4j-core'
    testRuntimeOnly 'org.apache.logging.log4j:log4j-slf4j2-impl'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
}

java {
    withJavadocJar()
    withSourcesJar()
}

flatbuffers {
    language = 'java'
}

task createFlatBuffers(type: FlatBuffers) {
    outputDir = file("build/generated/sources/flatbuffers")
}

compileJava {
    dependsOn += createFlatBuffers
}

test {
    useJUnitPlatform()
}

publishing {
    publications {
        myLibrary(MavenPublication) {
            from components.java
        }
    }
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/Uberfoo-Heavy-Industries/cpmutils"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}